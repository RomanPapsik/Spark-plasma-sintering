/PREP7

!-------------------------------------------------------------------------------
! DIMENSIONS OF SOLIDS
!-------------------------------------------------------------------------------

specimen_height = 6*millimeter
specimen_diameter = 36*millimeter

gap_thickness = 0.1*millimeter

punch_height = 30*millimeter
punch_diameter = 36*millimeter

die_height = 60*millimeter
die_bore = punch_diameter
die_diameter = 70*millimeter

inner_spacer_height = 40*millimeter
inner_spacer_diameter = 80*millimeter

middle_spacer_height = 20*millimeter
middle_spacer_diameter = 100*millimeter

outer_spacer_height = 20*millimeter
outer_spacer_diameter = 120*millimeter

electrode_height = 35*millimeter
electrode_bore = 0*millimeter
electrode_diameter = 120*millimeter

chamber_height = 250*millimeter
chamber_width = 280*millimeter

!-------------------------------------------------------------------------------
! POSITIONS
!-------------------------------------------------------------------------------

height_0 = 0
height_1 = specimen_height/2
height_2 = die_height/2
height_3 = specimen_height/2 + punch_height
height_4 = height_3 + inner_spacer_height
height_5 = height_4 + middle_spacer_height
height_6 = height_5 + outer_spacer_height
height_7 = height_6 + gap_thickness
height_8 = height_6 + electrode_height

height_load = height_8

!-------------------------------------------------------------------------------
! AREAS
!-------------------------------------------------------------------------------

! Specimen
ASEL,NONE
RECTNG,0,specimen_diameter/2,height_0,height_1
ARSYM,Y,ALL,,,,1,0
AATT,material_specimen

! Punch
ASEL,NONE
RECTNG,0,specimen_diameter/2,specimen_height/2,die_height/2
RECTNG,0,specimen_diameter/2,die_height/2,specimen_height/2 + punch_height
ARSYM,Y,ALL,,,,1,0
AATT,graphite

! Die
ASEL,NONE
RECTNG,specimen_diameter/2,die_diameter/2,0,specimen_height/2
RECTNG,specimen_diameter/2,die_diameter/2,specimen_height/2,die_height/2
ARSYM,Y,ALL,,,,1,0
AATT,graphite

! Inner spacer
ASEL,NONE
RECTNG,0,punch_diameter/2,height_3,height_4
RECTNG,punch_diameter/2,inner_spacer_diameter/2,height_3,height_4
ARSYM,Y,ALL,,,,1,0
AATT,graphite

! Middle spacer
ASEL,NONE
RECTNG,0,punch_diameter/2,height_4,height_5
RECTNG,punch_diameter/2,inner_spacer_diameter/2,height_4,height_5
RECTNG,inner_spacer_diameter/2,middle_spacer_diameter/2,height_4,height_5
ARSYM,Y,ALL,,,,1,0
AATT,graphite

! Outer spacer
ASEL,NONE
RECTNG,0,punch_diameter/2,height_5,height_6
RECTNG,punch_diameter/2,inner_spacer_diameter/2,height_5,height_6
RECTNG,inner_spacer_diameter/2,middle_spacer_diameter/2,height_5,height_6
RECTNG,middle_spacer_diameter/2,outer_spacer_diameter/2,height_5,height_6
ARSYM,Y,ALL,,,,1,0
AATT,graphite

! Foil
ASEL,NONE
RECTNG,0,punch_diameter/2,height_6,height_7
RECTNG,punch_diameter/2,inner_spacer_diameter/2,height_6,height_7
RECTNG,inner_spacer_diameter/2,middle_spacer_diameter/2,height_6,height_7
RECTNG,middle_spacer_diameter/2,outer_spacer_diameter/2,height_6,height_7
ARSYM,Y,ALL,,,,1,0
AATT,graphite ! Bagheri claims foil but did not use it

! Electrode
ASEL,NONE
RECTNG,0,punch_diameter/2,height_7,height_8
RECTNG,punch_diameter/2,inner_spacer_diameter/2,height_7,height_8
RECTNG,inner_spacer_diameter/2,middle_spacer_diameter/2,height_7,height_8
RECTNG,middle_spacer_diameter/2,outer_spacer_diameter/2,height_7,height_8
ARSYM,Y,ALL,,,,1,0
AATT,inconel

! Merge keypoints and areas
ALLSEL,ALL
NUMMRG,KP,0.001*millimeter
APLOT
