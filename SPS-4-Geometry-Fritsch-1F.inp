/PREP7

!-------------------------------------------------------------------------------
! DIMENSIONS OF SOLIDS
!-------------------------------------------------------------------------------

*IF,status_thickness_specimen,EQ,undefined,THEN
	specimen_thickness = 3*millimeter
*ELSE
	specimen_thickness = thick*millimeter
*ENDIF
specimen_diameter = 20*millimeter

graphite_paper_thickness = 0.25*millimeter
cfc_foil_thickness = 0.2*millimeter
felt_thickness = 10*millimeter

die_height = 50*millimeter
die_diameter = 50*millimeter
die_bore = 20*millimeter

electrode_height = 70*millimeter
electrode_diameter = 100*millimeter
electrode_bore = 0*millimeter

punch_height = 37*millimeter
punch_diameter = 20*millimeter

retainer_electrode_height = 25*millimeter
retainer_electrode_depth = 15*millimeter
retainer_electrode_bore = 44*millimeter
retainer_electrode_diameter = 110*millimeter

chamber_height = 280*millimeter
chamber_width = 280*millimeter

probe_depth = 10*millimeter

!-------------------------------------------------------------------------------
! POSITIONS
!-------------------------------------------------------------------------------

radius_0 = 0
radius_1 = specimen_diameter/2
radius_2 = specimen_diameter/2 + graphite_paper_thickness
radius_3 = die_diameter/2
radius_4 = retainer_electrode_bore/2
radius_5 = electrode_diameter/2
radius_6 = retainer_electrode_diameter/2

height_0 = 0
height_1 = specimen_thickness/2
height_2 = specimen_thickness/2 + graphite_paper_thickness
height_3 = die_height/2
height_4 = specimen_thickness/2 + graphite_paper_thickness + punch_height
height_5 = height_4 + electrode_height + cfc_foil_thickness - retainer_electrode_depth
height_6 = height_4 + electrode_height
height_7 = height_4 + electrode_height + cfc_foil_thickness
height_8 = height_5 + retainer_electrode_height

height_load = height_8

!-------------------------------------------------------------------------------
! AREAS
!-------------------------------------------------------------------------------

! Specimen
ASEL,NONE
RECTNG,0,specimen_diameter/2,0,specimen_thickness/2
ARSYM,Y,ALL,,,,1,0
AATT,material_specimen

! Graphite foil between specimen, dies and punches
ASEL,NONE
RECTNG,0,specimen_diameter/2,height_1,height_2
RECTNG,specimen_diameter/2,radius_2,height_0,height_1
RECTNG,specimen_diameter/2,radius_2,height_1,height_2
RECTNG,specimen_diameter/2,radius_2,height_2,height_3
ARSYM,Y,ALL,,,,1,0
*IF,add_graphite_paper,EQ,true,THEN
	AATT,graphite_paper
*ELSE
	AATT,graphite
*ENDIF

! Die
ASEL,NONE
RECTNG,radius_2,die_diameter/2,height_0,height_1
RECTNG,radius_2,die_diameter/2,height_1,height_2
RECTNG,radius_2,die_diameter/2,height_2,height_3
ARSYM,Y,ALL,,,,1,0
AATT,graphite

! Punch
ASEL,NONE
RECTNG,radius_0,radius_1,height_2,height_3
RECTNG,radius_0,radius_1,height_3,height_4
ARSYM,Y,ALL,,,,1,0
AATT,graphite

! Electrode
ASEL,NONE
RECTNG,                        0,         punch_diameter/2,height_4,height_5
RECTNG,         punch_diameter/2,retainer_electrode_bore/2,height_4,height_5
RECTNG,retainer_electrode_bore/2,     electrode_diameter/2,height_4,height_5
RECTNG,                        0,         punch_diameter/2,height_5,height_6
RECTNG,         punch_diameter/2,retainer_electrode_bore/2,height_5,height_6
RECTNG,retainer_electrode_bore/2,     electrode_diameter/2,height_5,height_6
ARSYM,Y,ALL,,,,1,0
AATT,graphite

! CFC foil
ASEL,NONE
RECTNG,retainer_electrode_bore/2,electrode_diameter/2,height_6,height_7
ARSYM,Y,ALL,,,,1,0
*IF,add_cfc_foil,EQ,true,THEN
	AATT,cfc_foil
*ELSE
	AATT,inconel
*ENDIF

! Retainer of electrode
ASEL,NONE
RECTNG,retainer_electrode_bore/2,electrode_diameter/2,height_7,height_8
RECTNG,electrode_diameter/2,retainer_electrode_diameter/2,height_7,height_8
RECTNG,electrode_diameter/2,retainer_electrode_diameter/2,height_6,height_7
RECTNG,electrode_diameter/2,retainer_electrode_diameter/2,height_5,height_6
ARSYM,Y,ALL,,,,1,0
AATT,inconel

! Felt on the die
*IF,add_felt_die,EQ,true,THEN
	ASEL,NONE
	RECTNG,die_diameter/2,die_diameter/2+felt_thickness,height_0,height_1
	RECTNG,die_diameter/2,die_diameter/2+felt_thickness,height_1,height_2
	RECTNG,die_diameter/2,die_diameter/2+felt_thickness,height_2,height_3
	ARSYM,Y,ALL,,,,1,0
	AATT,felt
*ENDIF

! Felt on the electrodes
*IF,add_felt_electrode,EQ,true,THEN
	ASEL,NONE
	RECTNG,retainer_electrode_diameter/2,retainer_electrode_diameter/2+felt_thickness,height_4,height_5
	ARSYM,Y,ALL,,,,1,0
	AATT,felt
*ENDIF

!-------------------------------------------------------------------------------
! CHAMBER
!-------------------------------------------------------------------------------

K,9001,-chamber_width/2,-chamber_height/2
K,9002,+chamber_width/2,-chamber_height/2
K,9003,+chamber_width/2,+chamber_height/2
K,9004,-chamber_width/2,+chamber_height/2
L,9001,9002
L,9002,9003
L,9003,9004
L,9004,9001

!-------------------------------------------------------------------------------
! FINALIZATION
!-------------------------------------------------------------------------------

! Merge keypoints and areas
ALLSEL,ALL
NUMMRG,KP,0.001*millimeter
APLOT
