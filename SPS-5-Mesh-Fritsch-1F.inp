/PREP7

!-------------------------------------------------------------------------------
! INTERIOR DISCRETIZATION
!-------------------------------------------------------------------------------

! 2D quadratic element
thermoelectric_element = 222
ET,thermoelectric_element,PLANE222
! Axisymmetric formulation
KEYOPT,thermoelectric_element,3,1
! Thermoelectric formulation
KEYOPT,thermoelectric_element,1,110

! Sizing
element_size = 1*millimeter
LSEL,ALL
LESIZE,ALL,element_size,,,,1,,,0
LESIZE,ALL,,,4,,2,,,0

! Meshing
TYPE,thermoelectric_element
MSHAPE,hexahedral_mesh,2D
MSHKEY,mapped_mesh
AMESH,ALL

! Merge everything
NUMMRG,ALL,0.001*millimeter

! Numbering
/PNUM,MAT,1
/NUMBER,1

!-------------------------------------------------------------------------------
! COMPONENTS
!-------------------------------------------------------------------------------

SELTOL,0.001*millimeter

! Water-cooled surface
KSEL,S,LOC,Y,-height_load,-height_load
KSEL,A,LOC,Y,+height_load,+height_load
LSLK,S,1
NSLL,S,1
CM,surface_water,NODE
ALLSEL,ALL

! Surface for radiation
KSEL,S,LOC,X,die_diameter/2,die_diameter/2
KSEL,R,LOC,Y,-height_3,height_3
LSLK,S,1
KSEL,S,LOC,Y,-height_3,-height_4
KSEL,A,LOC,Y,+height_3,+height_4
KSEL,R,LOC,X,punch_diameter/2,punch_diameter/2
LSLK,A,1
KSEL,S,LOC,X,electrode_diameter/2,electrode_diameter/2
KSEL,R,LOC,Y,-height_5,+height_5
LSLK,A,1
KSEL,S,LOC,X,retainer_electrode_diameter/2,retainer_electrode_diameter/2
LSLK,A,1
NSLL,S,1
CM,surface_radiation,NODE
ALLSEL,ALL

! Ground side
KSEL,S,LOC,X,retainer_electrode_bore/2,retainer_electrode_diameter/2
KSEL,R,LOC,Y,-height_load,-height_load
LSLK,S,1
NSLL,S,1
CM,surface_ground,NODE
ALLSEL,ALL

! Load side
KSEL,S,LOC,X,retainer_electrode_bore/2,retainer_electrode_diameter/2
KSEL,R,LOC,Y,+height_load,+height_load
LSLK,S,1
NSLL,S,1
CM,surface_load,NODE
ALLSEL,ALL
