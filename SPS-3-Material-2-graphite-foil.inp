!===============================================================================
! MATERIAL PROPERTIES
!===============================================================================

/PREP7

MPTGEN,1,100,270,30

!-------------------------------------------------------------------------------
! GRAPHITE FOIL
!-------------------------------------------------------------------------------

! Assign unique number
*SET,graphite_foil,2

! Heat capacity joule/(kilogram*kelvin)
*IF,machine_variant,EQ,Sinter_monoforce,THEN
	! Molenat (2010), Pavia (2013), Maniere (2016), Bagheri (2020)
	MP,C,graphite_foil,+34.27,2.72,-9.6E-4
*ELSE
	! Butland (1973)
	MP,C,graphite_foil,2.26975E3
	*DO,index,1,100
		T=270+(index-1)*30
		cp=2.26975E3
		cp=cp-6.01593E12/T**4
		cp=cp+6.66995E10/T**3
		cp=cp-1.81914E8/T**2
		cp=cp-3.77953E5/T
		cp=cp-1.016E-2*T
		MPDATA,C,graphite_foil,index,cp
	*ENDDO
	! Enweani
	! MP,C,graphite_foil,34.27,2.72,-0.00096
	! Antou (2009), Want (2010)
	! MP,C,graphite_foil,310.5,1.7
*ENDIF

! Thermal conductivity [watt/(meter*kelvin)]
! Maniere (2016), Bagheri (2020)
MP,KXX,graphite_foil,123,-0.0699,0.0000155

! Electrical resistivity [ohm*meter]
! Maniere (2016), Bagheri (2020)
MP,RSVX,graphite_foil,2.23E-4

! Density kilogram/(meter**3)
! Molenat (2010), Pavia (2013), Maniere (2016), Bagheri (2020)
MP,DENS,graphite_foil,1904,-0.0141

! Emisivity
MP,EMIS,graphite_foil,0.8
